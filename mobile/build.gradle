// Main build script for WebView App
// 
// Usage: gradlew assembleDebug           Build debug APK
//        gradlew assembleRelease         Build release APK

final VERSION_MAJOR = 3 // max two digits
final VERSION_MINOR = 8 // max two digits
final VERSION_PATCH = 8 // max two digits
final VERSION_BUILD = 0 // max three digits

apply plugin: "com.onesignal.androidsdk.onesignal-gradle-plugin"
apply plugin: "com.android.application"
apply from: "../utils.gradle"

android {
	compileSdkVersion 30
	buildToolsVersion "30.0.3"

	defaultConfig {
		applicationId "com.sikece.bekasi"
		minSdkVersion 19
		targetSdkVersion 30
		versionCode getVersionCode(VERSION_MAJOR, VERSION_MINOR, VERSION_PATCH, VERSION_BUILD)
		versionName getVersionName(VERSION_MAJOR, VERSION_MINOR, VERSION_PATCH)
		multiDexEnabled true
		vectorDrawables.useSupportLibrary = true
	}

	signingConfigs {
		release {
			def keystoreProperties = getKeystoreProperties file("../${project.property('keystore.properties')}")
			storeFile file("../${project.property('keystore.file')}")
			storePassword keystoreProperties["keystore.store.password"]
			keyAlias keystoreProperties["keystore.key.alias"]
			keyPassword keystoreProperties["keystore.key.password"]
		}
	}

	buildTypes {
		debug {
			buildConfigField "boolean", "LOGS", "true"
			buildConfigField "boolean", "DEV_ENVIRONMENT", "true"
			buildConfigField "boolean", "TEST_ADS", "false"

			versionNameSuffix "-debug"
		}

		release {
			buildConfigField "boolean", "LOGS", "false"
			buildConfigField "boolean", "DEV_ENVIRONMENT", "false"
			buildConfigField "boolean", "TEST_ADS", "false"

            zipAlignEnabled true
			minifyEnabled false
			shrinkResources false
			proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	setProperty("archivesBaseName", getArtifactName(defaultConfig))
}

dependencies {
	implementation fileTree(dir: "libs", include: ["*.jar"])
	implementation "androidx.appcompat:appcompat:1.2.0"
	implementation "androidx.fragment:fragment:1.3.2"
	implementation "androidx.multidex:multidex:2.0.1"
	implementation "com.google.android.ads.consent:consent-library:1.0.8"
	implementation "com.google.android.material:material:1.3.0"
	implementation "com.google.android.play:core:1.10.0"
	implementation "com.google.firebase:firebase-ads:19.8.0"
	implementation "com.google.firebase:firebase-analytics:18.0.2"
	implementation "com.google.firebase:firebase-messaging:21.0.1"
	implementation "com.onesignal:OneSignal:4.3.0"
	implementation "org.alfonz:alfonz-utility:0.9.3"
	implementation "org.alfonz:alfonz-view:0.9.3"
}

apply plugin: "com.google.gms.google-services"
